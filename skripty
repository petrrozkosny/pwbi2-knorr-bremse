dim_datumy = 

ADDCOLUMNS(
SELECTCOLUMNS(
CALENDARAUTO(),"datum",[Date]),
"rok",YEAR([datum]),
"mesic",MONTH([datum]),
"kvartal",QUARTER([datum]),
"cislo tydne",WEEKNUM([datum],21),
"den tydne",WEEKDAY([datum],2),
"nazev mesice",FORMAT([datum],"MMM"),
"nazev dne",FORMAT([datum],"DDD")
)
_________________________________________

01 suma srazek rok 2020 = 
-- takto ne
VAR _1 =
CALCULATE(
    SUM(data[Srazky]),
    FILTER(data,data[rok] = 2020))
-- takto ano, pokud nebude fungovat verze _3
VAR _2 = 
CALCULATE(
    SUM(data[Srazky]),
    FILTER(ALL(data[rok]),data[rok] = 2020))
-- takto ano
VAR _3 = 
CALCULATE(
    SUM(data[Srazky]),
    data[rok] = 2020)

return _3

____________________________________________________

02 suma srazek graz = 
CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita]= "GRAZ")

_______________________________________________

03 suma srazek vsechny roky = 
CALCULATE(
    SUM(data[Srazky]),
    all(dim_datumy[rok]))
_______________________________________________________

05 pf maximalni teplota = 

VAR max_teplota =max(data[Teplota])

VAR max_teplota_roky = 

MAXX(
ADDCOLUMNS(
    ALL(dim_datumy[rok]),
"maximalni rocni teplota",
CALCULATE(MAX(data[Teplota]))),
[maximalni rocni teplota])

return
if(max_teplota = max_teplota_roky,"CYAN")

_________________________________________________________________

06 pf rocni srazky = 
-- suma srazek v danem roce a lokalite
VAR rocni_srazky = SUM(data[Srazky])
VAR tabulka_srazek = 
ADDCOLUMNS(
    ALL(data[rok]),
"rocni srazky",
CALCULATE(SUM(data[Srazky])))

VAR min_rocni_srazky = MINX(tabulka_srazek,[rocni srazky])
VAR max_rocni_srazky = MAXX(tabulka_srazek,[rocni srazky])

RETURN
SWITCH(TRUE(),
rocni_srazky = max_rocni_srazky,"CYAN",
rocni_srazky = min_rocni_srazky,"BROWN",
"BLUE")

_____________________________________________________

07 den max srazek = 
VAR max_srazky = MAX(data[Srazky])
return
CALCULATE(MAX(data[Datum]),data[Srazky] = max_srazky)

_____________________________________________________

08 dny max srazek = 
CONCATENATEX(
    FILTER(data,
    data[Srazky] = MAX(data[Srazky])),
    data[Datum],", ")

________________________________________________

09 prumerne rocni srazky = 
AVERAGEX(
ADDCOLUMNS(
    ALL(data[rok]),
"suma rocni srazky",
CALCULATE(SUM(data[Srazky]))),
[suma rocni srazky])

________________________________________________________

10 suma srazek v poslednim roce = 

CALCULATE(
    SUM(data[Srazky]),
    data[rok] = MAX(data[rok]))

________________________________

11 suma srazek rok = 
IF(
    SELECTEDVALUE(data[mesic]) = 12,
    CALCULATE(SUM(data[Srazky]),ALL(data[mesic])),
    BLANK())
__________________________________________

=CUBEVALUE("ThisWorkbookDataModel";"[Measures].[suma srazek]";"[data].[mesic].["&C3&"]")

_______________________________________________________

12 nazev grafu = 
SWITCH(TRUE(),
HASONEFILTER(data[Lokalita]),
"Vývoj ročních srážek v lokalitě "&SELECTEDVALUE(data[Lokalita]),
ISFILTERED(data[Lokalita]),
"Vývoj ročních srážek v lokalitách "&
CONCATENATEX(DISTINCT(data[Lokalita]),data[Lokalita],", "),
"Vývoj ročních srážek ve všech lokalitách")

_______________________________________________________

zacatek mesice = EOMONTH(data[Datum],-1)+1 

_________________________________________________________________

Column = 
SWITCH(TRUE(),
[den tydne] <6 && [statni svatek] = FALSE(),[datum],
[den tydne]>5, CALCULATE(MIN(dim_datumy[datum]),dim_datumy[den tydne]<6 && dim_datumy[statni svatek] = FALSE() &&dim_datumy[datum] > EARLIER(dim_datumy[datum])))


______________________________________________________________

dim_datumy = 

ADDCOLUMNS(
ADDCOLUMNS(
SELECTCOLUMNS(
CALENDARAUTO(),"datum",[Date]),
"rok",YEAR([datum]),
"mesic",MONTH([datum]),
"kvartal",QUARTER([datum]),
"cislo tydne",WEEKNUM([datum],21),
"den tydne",WEEKDAY([datum],2),
"den",DAY([datum]),
"nazev mesice",FORMAT([datum],"MMM"),
"nazev dne",FORMAT([datum],"DDD")
),"statni svatek",
SWITCH(TRUE(),
[den] = 8 && [mesic] = 5,TRUE(),
[den] = 1 && [mesic] = 5,TRUE()))


